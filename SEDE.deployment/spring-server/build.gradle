plugins {
    id 'java'
    id 'groovy'

    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
}

group = 'de.upb.isys'
version = '0.0.10'

repositories {
    mavenLocal()

    jcenter()
    mavenCentral()

    maven {
        url "https://nexus.cs.upb.de/repository/maven-releases/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    /*
     * Logging dependencies
     */
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    testRuntimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.1'


    /*
     * SEDE dependencies
     */
    implementation 'de.upb.isys:SEDE.core:0.0.10'
    implementation 'de.upb.isys:SEDE.deployment:0.0.10'

    /*
     * Spring boot dependencies
     */
    implementation 'org.springframework.boot:spring-boot-dependencies:2.0.5.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    components {
        withModule('org.springframework:spring-beans') {
            allVariants {
                withDependencyConstraints {
                    // Need to patch constraints because snakeyaml is an optional dependency
                    it.findAll { it.name == 'snakeyaml' }.each { it.version { strictly '1.19' } }
                }
            }
        }
    }
    /*
     * Swagger dependencies
     */
    implementation 'io.springfox:springfox-swagger2:2.7.0'
    implementation 'com.github.joschi.jackson:jackson-datatype-threetenbp:2.6.4'
    implementation 'javax.validation:validation-api'

    /*
     * UI dependencies
     */
    def experimentalUi = true
    if (! experimentalUi) {
        implementation 'io.springfox:springfox-swagger-ui:2.7.0'
    } else {
        implementation 'io.springfox.ui:springfox-swagger-ui-rfc6570:1.0.0'
    }

    /*
     * Project dependencies
     */
    implementation 'com.google.guava:guava:26.0-jre'

    /*
     * Test framework dependencies
     */
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.4'
    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
    testImplementation 'junit:junit:4.12'
}


bootJar {
    mainClassName = 'de.upb.sede.edd.SpringBootApp'
}
