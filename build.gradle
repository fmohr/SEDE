plugins {
    id "net.linguica.maven-settings" version "0.5" apply false

    // https://plugins.gradle.org/plugin/org.ec4j.editorconfig
    id "org.ec4j.editorconfig" version "0.0.3"
}


subprojects {
    /*
        configuration for all sub projects.
     */
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'net.linguica.maven-settings'
    apply plugin: 'eclipse'

    group = 'de.upb.isys'
    version = '0.1.0'

    configurations {
        provided
        compile.extendsFrom provided
        serviceConf
    }

    project.ext {
        /*
         * This flag determines weather the subproject should be published to the maven repository.
         */
        toBePublished = true
        /*
         * Set of library versions used by SEDE projects. Updating these will propagate to most sub-projects.
         */
        project.ext.libs = [
            /*
             * Testing
             */
            spock: 'org.spockframework:spock-core:1.3-groovy-2.5',
            groovy: 'org.codehaus.groovy:groovy-all:2.5.7',
            junit: 'junit:junit:4.12',
            hamcrest: 'org.hamcrest:hamcrest-all:1.3',

            /*
             * Logging
             */
            log4j: [group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.1'],
            slf4j: [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'],

            /*
             * Commons
             */
            commons: [group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'],
            commons_io: [group: 'commons-io', name: 'commons-io', version: '2.6'],

            guava: 'com.google.guava:guava:28.0-jre',

            /*
             * Jackson
             */
            jackson_databind: [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9'],
            jackson_dataformat: 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.9.3',
            jackson_schema: 'com.fasterxml.jackson.module:jackson-module-jsonSchema:2.10.0.pr2',

            /*
             * Json Simple
             * Apply both in one:
             * >> compile libs.json_simple, libs.json_simple_exclusions
             */
            json_simple: [group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'],
            json_simple_exclusions: {
                exclude module: 'junit'
                exclude group: 'org.hamcrest'
            },

            immutables: 'org.immutables:value:2.7.4',
            findbugs:[group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'],
            jgit: [group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.3.1.201904271842-r'],
            okhttp3: [group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.2.0'],
            owner: [group: 'org.aeonbits.owner', name: 'owner', version: '1.0.8'],
            systemrule: 'com.github.stefanbirkner:system-rules:1.19.0',
        ]
        isyslibs = [
            demolib: [group: 'de.upb.isys', name: 'demolib', version: '1.0'],
            builtins: [group: 'de.upb.isys', name: 'builtins', version: '1.1']
        ]
    }

    repositories {
        mavenLocal()

        jcenter()
        mavenCentral()

        maven {
            url "https://nexus.cs.upb.de/repository/maven-releases/"
        }
        // we use jitpack in order to add dependecy to github projects:
        maven { url 'https://jitpack.io' }

    }

    dependencies {
		compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-slf4j-impl
        testRuntimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.1'


        // https://mvnrepository.com/artifact/org.hamcrest/hamcrest-all
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile (
                'junit:junit:4.12',
                'org.codehaus.groovy:groovy-all:2.4.16',
                'org.spockframework:spock-core:1.1-groovy-2.4'
        )
    }

    // source Jar:
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    // build Jar:
    task jarjar(type: Jar, dependsOn: [assemble]) {

        manifest {
            attributes 'Implementation-Title': "${project.name}",
                    'Implementation-Version': version
        }
        destinationDir = file("$rootDir/deploy/SEDE")
        baseName = project.name

        from { ((configurations.compile - configurations.provided))
                .findAll { it.name.endsWith('jar') }
                .collect { zipTree(it) } }
        from sourceSets.main.output
    }

    // copy python source:
    task buildPython(type: Copy) {
        from "${projectDir}/src/main/python/"
        include "**/*.py"
        into "${rootDir}/deploy/SEDE_python/${project.name}"
    }

    clean.doFirst {
        delete fileTree("$rootDir") {
            include '*.jar'
        }
        delete "${projectDir}/testrsc"
        delete "${projectDir}/out"
        delete "${projectDir}/instances"
        delete "${projectDir}/servicespec"
        delete "${rootDir}/deploy/SEDE"
        delete "${rootDir}/deploy/SEDE_Python"
    }

    apply from: "$rootDir/publish.gradle"
}

apply from: 'docker.gradle'

editorconfig {

    // Exclude folder from editorconfig
    excludes = ['**/instances', 'LICENSE', '**/__pycache__', '**/*.pyc', '**/semantic-data']

}
